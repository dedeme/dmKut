// Copyright 07-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

import "data/doc";
import "data/docEntry";
import "data/dpath";
import "db";
import "db/pathsTb";

/// Module reader.

// \<file>, s -> [s, s] | [] :: [doc, line] | []
readDoc = \:file f, l -> {
  // \[s]|[] -> s
  findLine = \lOp -> {
    if (!lOp) return "";
    if (str.trim(lOp!) != "") return lOp!;
    while () {
      lOp = f.readLine();
      if (!lOp) return "";
      if (str.trim(lOp!) != "") return lOp!;
    }
  };

  lV = [l];
  while () {
    Bf = []; // [s.]
    ltrim = str.ltrim(lV!);

    if (ltrim.starts("///")) {
      lOp = [];
      ltrimV = [ltrim];
      while () {
        if (str.len(ltrimV!) == 3) Bf.push("");
        else {
          ch = ltrim[3];
          Bf.push(ch <= " " ? ltrimV![4:] : ltrimV![3]);
        }
        nlOp = f.readLine();
        lOp.clear();
        if (!!nlOp) arr.push(lOp, nlOp!);
        if (!lOp | !str.starts(str.trim(lOp!), "///")) break;
        ltrimV! = str.ltrim(lOp!);
      }
      lV! = findLine(lOp);
      if (str.starts(str.ltrim(lV!), "/")) continue;
      else return [Bf.join("\n"), lV!];
    }

    if (ltrim.starts("/**")) {
      lOp = f.readLine();
      if (!lOp) return [];
      ltrim = str.ltrim(lOp!);
      if (ltrim.index("*/") != -1) return [];
      strip = str.len(lOp!) - ltrim.len();

      ltrimV = [ltrim];
      while () {
        Bf.push(ltrimV!);
        nlOp = f.readLine();
        arr.clear(lOp);
        if (!!nlOp) arr.push(lOp, nlOp!);
        if (!lOp | str.index(lOp!, "*/") != - 1) break;
        l = lOp!;
        ltrimV! = str.len(l) < strip ? "" : l[strip:];
      }
      lV! = findLine(f.readLine());
      if (str.starts(str.ltrim(lV!), "/")) continue;
      else return [Bf.join("\n"), lV!];
    }

    return [];
  }
};

// Returns the position of the first character of 'chs' (ch).
//    tx: Text for searching.
//    chs: Characters to find.
// \s, s -> n
indexes = \tx, chs -> {
  Ixs = []; // [n]
  for (i = 0:str.len(chs)) Ixs.push(str.index(tx, chs[i]));

  ixV = [-1];
  for (ix = Ixs)
    if (ix > -1 & (ixV! == -1 | ix < ixV!)) ixV! = ix;
  return ixV!;
};

// \s -> s
mkLink = \s -> {
  ix0 = indexes(s, "\n;=(");
  ixV = [ix0 == -1 ? str.len(s) : ix0];
  ch = ix0 == -1 ? "" : s[ix0];

  if (ch == "(" & str.starts(s[ix0:], "(*")) {
    ix2 = str.index(s[ix0 + 1:], "(");
    ixV! = ix2 == -1 ? ixV! : ix2 + 2;
  }
  r = str.replace(s[:ixV!], "#", "_");

  Bf = []; //[s.];
  for (i = 0:r.len()) if (r[i] > " ") Bf.push(r[i]);
  return Bf.join("");
};

// \s -> b
isL = \ch ->
  return (ch >= 'a' & ch <= 'z') |
    (ch >= 'A' & ch <= 'Z') | ch == '_'
  ;;

// \s -> b
isLD = \ch -> return isL(ch) | (ch >= '0' & ch <= '9');;

// \s, n -> s
readName = \tx, i -> {
  len = str.len(tx);
  startV = [i];
  while (startV! < len & !isL(tx[startV!])) startV! += 1;
  if (startV! < len) {
    endV = [startV!];
    while (endV! < len & isLD(tx[endV!])) endV! += 1;
    return tx[startV!:endV!];
  }
  return "???";
};

// \s, n -> s
readNameBk = \tx, i -> {
  iV = [i - 1];
  while (iV! >= 0 & !isLD(tx[iV!])) iV! -= 1;
  if (iV! >= 0) {
    end = iV! + 1;
    iV! -= 1;
    while (iV! >= 0 & isLD(tx[iV!])) iV! -= 1;
    return tx[iV! + 1:end];
  }
  return "???";
};

// \<file>, s, b -> s
readCode = \:file f, line, isFirst -> {
  l = str.ltrim(line);

  if (isFirst & l.starts("#ifndef ")) return "overview";

  Bf = []; // [s.]

  if (l.starts("#define ")) {
    lV = [l];
    while () {
      l = lV!;
      Bf.push(l + "\n");
      if (l.ends("\\")) {
        lOp = f.readLine();
        if (!!lOp) {
          lV! = lOp!;
          continue;
        }
      }
      return Bf.join("");
    }
  }
  // else

  if (l.starts("#")) return "";


  withBracketsV = [0];
  lineOp = [line];
  while (!!lineOp) {
    line = lineOp!;
    if (withBracketsV! == 0) {
      ix = indexes(line, ";{");
      if (ix == -1) {
        Bf.push(line + "\n");
        newLineOp = file.readLine(f);
        lineOp.clear();
        if (!!newLineOp) lineOp.push(newLineOp!);
        continue;
      }
      // else

      ch = line[ix];
      ix1 = ix + 1;
      Bf.push(line[:ix1]);
      if (ch == ';') return Bf.join("");

      lineOp! = line[ix1:];
      withBracketsV! = 1;
      continue;
    }
    // else

    ix = indexes(line, "{}");
    if (ix == -1) {
      Bf.push(line + "\n");
      newLineOp = f.readLine();
      lineOp.clear();
      if (!!newLineOp) lineOp.push(newLineOp!);
      continue;
    }
    // else

    ch = line[ix];
    ix1 = ix + 1;
    Bf.push(line[:ix1]);
    withBracketsV! += ch == "{" ? 1 : -1;

    lineOp! = line[ix1:];
  }

  return "";
};

/// \s, s -> [<doc>] | []
read = \pack, pt -> {
  ppathOp = arr.find(
    pathsTb.read(), \:dpath p -> return p.id == pack;
  );
  if (!ppathOp) return [];
  :dpath ppath = ppathOp!;

  fpath = path.cat([ppath.spath, "include", pt + ".h"]);
  if (!file.exists(fpath)) return [];

  :doc dc = doc.new("", [], [], [], [], [], [], []);

  f = file.ropen(fpath);
  isFirstV = [true];
  while () {
    lOp = file.readLine(f);
    if (!lOp) break;
    l = lOp!;

    if (str.starts(str.trim(l), "/")) {
      docRs = readDoc(f, l);
      if (!!docRs & docRs[1] != "") {
        cd = readCode(f, docRs[1], isFirstV!);
        cdTrim = str.trim(cd);
        d = docRs[0];

        isFirstV! = false;

        if (cdTrim == "overview") {
          dc.docTx = d;
        } else if (cdTrim.starts("#define ")) {
          arr.push(dc.defines, docEntry.new(
            readName(cdTrim, str.len("#define ")),
            d, cd, mkLink(cdTrim)
          ));
        } else if (cdTrim.starts("typedef ")) {
          ix0 = str.index(cdTrim, ')');
          ix = ix0 == -1 ? str.len(cdTrim) : ix0;
          arr.push(dc.typedefs, docEntry.new(
            readNameBk(cdTrim, ix),
            d, cd, mkLink(cdTrim)
          ));
        } else if (cdTrim.index('{') != -1) {
          if (str.starts(cd, "enum ")) {
            arr.push(dc.enums, docEntry.new(
              readName(cdTrim, str.len("enum ")),
              d, cd, mkLink(cdTrim)
            ));
          }
          if (cdTrim.starts("struct ")) {
            arr.push(dc.structs, docEntry.new(
              readName(cdTrim, str.len("struct ")),
              d, cd, mkLink(cdTrim)
            ));
          }
          if (cdTrim.starts("union ")) {
            arr.push(dc.unions, docEntry.new(
              readName(cdTrim, str.len("union ")),
              d, cd, mkLink(cdTrim)
            ));
          }
        } else {
          ix = cdTrim.index('=');
          if (ix != -1) {
            arr.push(dc.vars, docEntry.new(
              readNameBk(cdTrim, ix),
              d, cd, mkLink(cdTrim)
            ));
          } else {
            ix = cdTrim.index('(');
            if (ix != -1) {
              ixV = [ix];
              if (str.starts(str.trim(cdTrim[ix + 1:]), "*"))
                ixV! = str.indexFrom(cdTrim, ix, ')');
              arr.push(dc.functions, docEntry.new(
                readNameBk(cdTrim, ixV!),
                d, cd, mkLink(cdTrim)
              ));
            } else if (cdTrim != "") {
              arr.push(dc.vars, docEntry.new(
                readNameBk(cdTrim, str.len(cdTrim)),
                d, cd, mkLink(cdTrim)
              ));
            }
          }
        }
      }
    }
  }

  return [dc];
};
