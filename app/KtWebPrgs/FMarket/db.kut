// Copyright 24-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Data base of jstb's.

import "libdm/log";
import "libmkt/qs";
import "libmkt/quotes";
import "libmkt/model";
import "libmkt/models";
import "cts";
import "data/flea";
import "data/summaryRs";

modelsPath = path.cat([cts.dbPath, "models"]);

// \s -> s
modelPath = \mdId -> return path.cat([modelsPath, mdId]);;

// \s -> s
fleasPath = \mdId -> return path.cat([modelPath(mdId), "fleas.tb"]);;

// \s -> s
historicPath = \mdId -> return path.cat([modelPath(mdId), "historic"]);;

// \s, s -> s
rkPath = \mdId, date -> return path.cat(
  [historicPath(mdId), date, "rk.tb"]
);;

// \s, s -> s
bwaPath = \mdId, date -> return path.cat(
  [historicPath(mdId), date, "bestWorstAvg.tb"]
);;

// Read companies.
// \ -> [s.]
readCos = \ -> {
  CosT = js.r(file.read(path.cat([cts.marketDataPath, "ibex.tb"])));
  :arr Cos = CosT[0];
  Cos.sort(\c1, c2 -> return c1 < c2;);
  return Cos;
};

/// Read CMarket quotes.
/// \ -> <quotes>
readQuotes = \ -> {
  dpath = path.cat([cts.marketDataPath, "quotes"]);
  Cos = readCos();
  return quotes.read(dpath, Cos);
};

/// Returns the model list.
modelsList = \ -> return models.list();;

/// Reinitializes database.
/// It must be called when a new model is added.
init = \ -> {
  log.reset();

  QsOp = [];

  file.mkdir(cts.dbPath);

  file.mkdir(modelsPath);
  for (mdId = arr.map(modelsList(), \:model md -> return md.id;)) {
    mdPath = modelPath(mdId);
    file.mkdir(mdPath);
    file.mkdir(path.cat([mdPath, "historic"]));
    fsPath = fleasPath(mdId);
    if (!file.exists(fsPath)) {
      newFs = arr.new(cts.fleasPerModel, 0);
      for (i = 0:cts.fleasPerModel)
        newFs[i] = flea.generate(i, 0, mdId);
      if (!QsOp) arr.push(QsOp, readQuotes());
      Qs = QsOp!;
      writeFleas(mdId, flea.cycle(newFs, Qs));
    }
  }
};

/// Reads 'fleas.tb' in model 'mdId'.
/// Returns a fleas array sorted by evaluation points (descending).
///   mdId: Model identifier.
/// \s -> [<flea>.]
readFleas = \mdId -> {
  return arr.map(js.r(file.read(fleasPath(mdId))), flea.fromJs);
};

/// Writes 'fleas.tb' in model 'mdId'.
///   mdId : Model identifier.
///   fleas: Fleas array sorted by evaluation points (descending).
/// \s, [<flea>.] -> ()
writeFleas = \mdId, Fleas -> {
  now = time.now();
  today = time.toStr(now);

  file.write(fleasPath(mdId), js.w(arr.map(Fleas, flea.toJs)));

  file.mkdir(historicPath(mdId));
  rkPath = rkPath(mdId, today);
  file.mkdir(path.parent(rkPath));
  file.write(rkPath, js.w(arr.map(Fleas[:40], flea.toJs)));

  :flea f = Fleas[0];
  BWA = [
    summaryRs.new(f.assets, f.profits, f.points),
    summaryRs.new(f.assets, f.profits, f.points),
    summaryRs.new(0.0, 0.0, 0)
  ];

  for (:flea f = Fleas) {
    :summaryRs b = BWA[0];
    B2 = summaryRs.new(
      f.assets > b.assets ? f.assets : b.assets,
      f.profits > b.profits ? f.profits : b.profits,
      f.points > b.points ? f.points : b.points
    );
    :summaryRs w = BWA[1];
    W2 = summaryRs.new(
      f.assets < w.assets ? f.assets : w.assets,
      f.profits < w.profits ? f.profits : w.profits,
      f.points < w.points ? f.points : w.points
    );
    :summaryRs a = BWA[2];
    A2 = summaryRs.new(
      a.assets + f.assets,
      a.profits + f.profits,
      a.points + f.points
    );
    BWA[0] = B2;
    BWA[1] = W2;
    BWA[2] = A2;
  }
  size = arr.size(Fleas);
  sizef = math.itof(size);
  :summaryRs a = BWA[2];
  A2 = summaryRs.new(a.assets / sizef, a.profits / sizef, a.points / size);
  BWA[2] = A2;

  file.write(bwaPath(mdId, today), js.w(BWA));

  // CLEAN

  :arr Dates = file.dir(historicPath(mdId));
  Dates.sort(\d1, d2 -> return d1 > d2;);
  minusYear = time.toStr(time.addDays(now, -365));

  dateV = ["11111111"];
  for (i, d = Dates) {
    if (i < 5) {
      dateV! = d;
      continue;
    }
    if (d > minusYear) {
      if (d[:6] == dateV![:6])
        file.del(path.cat([historicPath(mdId), d]));
    } else {
      if (d[:4] == dateV![:4])
        file.del(path.cat([historicPath(mdId), d]));
    }
    dateV! = d;
  }
};

/// Returns an dictionary {date -> [flea.]} with rankings of a model.
/// \s -> {[<flea>.].}
readRankings = \mdId -> {
  Dates = file.dir(historicPath(mdId));
  Rk = {}; // {[<flea>.].} :: {date -> [flea.]}
  for (d = Dates)
    Rk.put(d, arr.map(js.r(file.read(rkPath(mdId, d))), flea.fromJs));
  return Rk;
};

/// Returns an dictionary {date -> {model -> [best, worst, medium]}}, where
/// best, worst and medium are arrays [assets, profits, points].
/// \ -> {{[[f,f,f], [f,f,f], [f,f,f]].}.}
readSummary = \ -> {
  R = {}; // {{[[f, f, f],[f, f, f],[f, f, f]].}.}
  Models = file.dir(modelsPath);
  for (mdId = Models) {
    Dates = file.dir(historicPath(mdId));
    for (d = Dates) {
      ModelDicOp = R.get(d);
      :dic ModelDic = !ModelDicOp ? {} : ModelDicOp!;
      ModelDic.put(mdId, js.r(file.read(bwaPath(mdId, d))));
      R.put(d, ModelDic);
    }
  }
  return R;
};
