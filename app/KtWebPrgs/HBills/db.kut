// Copyright 16-Nov-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Data base.

import "KtWeb/global";
import "libdm/jstb";
import "data/cts";
import "data/accAnn";
import "data/monthAnn";

// EXTERN DATA BASE

// Current year
// i
year = time.year(time.now());

// Years range (from [0] to [1] both inclusive - see 'readBills()')
// [i, i]
YearsRange = [
  (year - 5 > 2022) ? year - 5 : 2022,
  year
];

/// Read hollidays (bills) annotations.
/// \-> [<monthAnn>...]
readBills = \ -> {
  R = [];
  Month = [math.toStr(YearsRange[0]) + "01"];
  Sum = [0.0];
  for (year = YearsRange[0] : YearsRange[1] + 1) {
    y = math.toStr(year) + ".db";
    pth = path.cat([cts.accPath, y]);
    A = js.ra(file.read(pth));
    for (annJs = js.ra(A[3])) {
      Fields = js.ra(annJs);
      Am = map.get(js.ro(Fields[2]), cts.billsAcc);
      Am2 = map.get(js.ro(Fields[3]), cts.staysAcc);
      if (!arr.empty(Am) & arr.empty(Am2)) {
        mdate = js.rs(Fields[0])[0:6];
        if (mdate > Month!) {
          arr.push(R, monthAnn.new(Month!, [], Sum!));
          Month! = mdate;
          Sum! = js.rf(Am!);
        } else {
          Sum! += js.rf(Am!);
        }
      }
    }
  }
  arr.push(R, monthAnn.new(Month!, [], Sum!));
  return arr.filter(R, \a -> return a[2] > 0.0;);
};

/// Read hotels (stays) annotations.
/// \-> [<accAnn>, ...]
readStays = \ -> {
  R = [];
  for (year = YearsRange[0] : YearsRange[1] + 1) {
    y = math.toStr(year) + ".db";
    pth = path.cat([cts.accPath, y]);
    A = js.ra(file.read(pth));
    for (annJs = js.ra(A[3])) {
      Fields = js.ra(annJs);
      Am = map.get(js.ro(Fields[2]), cts.staysAcc);
      if (!arr.empty(Am)) {
        arr.push(
          R,
          accAnn.new(js.rs(Fields[0]), js.rs(Fields[1]), js.rf(Am!))
        );
      }
    }
  }
  return R;
};

// PROGAM DATA BASE

DbPath = [""];

/// Initializes data base.
/// \-> ()
init = \ -> {
  dbPath = path.cat([global.cgiRoot, cts.appName]);
  DbPath! = dbPath;
  if (!file.exists(dbPath)) file.mkdir(dbPath);
};

/// Returns the table of places in "places.tb"
/// \-> m[s]<jsdb>
placesTb = \-> return jstb.new(
  DbPath! + "/places.tb",
  \-> return "{}";,
  \P -> return js.wo(map.fromArr(arr.mp(
      map.toArr(P),
      \A -> return [A[0], js.ws(A[1])];
    )));,
  \j -> return map.fromArr(arr.mp(
      map.toArr(js.ro(j)),
      \A -> return [A[0], js.rs(A[1])];
    ));
);;
