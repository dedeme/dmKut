// Copyright 25-Apr-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Database

import "cts";

/// Read all data base.
/// 'data' is a JSON string.
/// \ -> {timeStamp:s, year:s, Years:[s.], data:s}
readData = \ -> {
	dir = cts.dataPath;
	if (!file.exists(dir)) {
		file.mkdir(dir);
		:time dt = time.now();
		year = dt.fmt("%Y");
		file.write(
			file.cat([dir, "control.tb"]),
      js.w(["", year])
		);
		file.write(file.cat([dir, year + ".db"]), js.w([]));
	}

	A = js.r(file.read(file.cat([dir, "control.tb"])));
	year0 = A[1];
	Years = []; // [s.]
	for (fname = file.dir(dir))
		if (str.ends(fname, ".db")) Years.push(fname[0:-3]);
	Years.sort(\y1, y2 -> return y1 < y2;);

  year = Years.any(\y -> return y == year0;) ? year0 : Years.peek();

	return {
    timeStamp: A[0],
    year,
    Years,
    data: file.read(file.cat([dir, year + ".db"]))
  };
};

/// Writes all data base and returns a new timeStamp or "" if data is out of date.
///   timeStamp: Update control.
///   year: Edited year.
///   data: JSON string with year annotations.
/// \s, s, s -> s
writeData = \timeStamp, year, data -> {
  controlTb = file.cat([cts.dataPath, "control.tb"]);
  A = js.r(file.read(controlTb));
  if (A[0] != timeStamp) return "";

  newTimeStamp = cryp.genK(20);
  file.write(controlTb, js.w([newTimeStamp, year]));
  file.write(file.cat([cts.dataPath, year + ".db"]), data);
  return newTimeStamp;
};

/// Changes the current year.
///   timeStamp: Update control.
///   year: Edited year.
/// \s, s -> s
changeYear = \timeStamp, year -> {
  controlTb = file.cat([cts.dataPath, "control.tb"]);
  A = js.r(file.read(controlTb));
  if (A[0] != timeStamp) return "";

  newTimeStamp = cryp.genK(20);
  file.write(controlTb, js.w([newTimeStamp, year]));
  return newTimeStamp;
};
