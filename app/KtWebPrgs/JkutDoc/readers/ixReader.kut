// Copyright 08-May-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Package index reader.

import "libdm/jstb";
import "db";
import "data/dpath";
import "data/indexTree";

// Read module documentation.
// \s -> s
docRead = \fpath -> {
  if (!file.exists(fpath)) return "";

  R = [""];
  f = file.ropen(fpath);
  while () {
    L = file.readLine(f);
    if (arr.size(L) == 0) break;

    l = str.trim(L!);
    if (str.starts(l, "import ")) break;
    if (str.starts(l, "///")) {
      R! = l[4:];
      break;
    }
  }
  file.close(f);
  return R!;
};

// Adds trees to node
// \[m], s -> ()
completeTrees = \Trees, dir -> {
  Fnames = file.dir(dir);
  for (fname = Fnames) {
    fpath = path.cat([dir, fname]);
    if (file.isDirectory(fpath)) {
      :indexTree NewTree = indexTree.mk(fname, [], []);
      arr.push(Trees, NewTree);
      completeTrees(NewTree.Trees, fpath);
    } else {
      if (!str.ends(fname, ".jkut")) continue;
      arr.push(Trees, indexTree.mk(fname[:-5], [docRead(fpath)], []));
    }
  }
};

/// Reads package index tree.
/// \s -> [m]
read = \pack -> {
  ppathOp = arr.find(
    db.pathsTb()[jstb.read](), \:dpath p -> return p.id == pack;
  );
  if (!ppathOp) return [];
  :dpath ppath = ppathOp!;

  root = ppath.spath;
  if (!file.exists(root)) return [];

  :indexTree R = indexTree.mk("", [], []);
  completeTrees(R.Trees, root);
  return [R];
};
