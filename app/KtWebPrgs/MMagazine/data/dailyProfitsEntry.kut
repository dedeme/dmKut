// Copyright 03-May-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Daily profits data.

import "cts";
import "data/profitsEntry";

/// Creates a new daily profits data.
///   date: Date of calculation.
///   profits: Profits on date (amount)
///   cash: Cash on date (amount)
///   risk: Minimum future profits on date (amount)
/// \s, f, f, f -> <dailyProfitsEntry>
new : date, profits, cash, risk;

/// Calculate weekly profits of investors.
///   invPs: Daily profits of investors.
/// \[<dailyProfitsEntry>.] -> [<profitsEntry>.]
mkProfits = \InvP -> {
  // [<ProfitsEntry>.]
  R = [];

  now = time.now();
  Date = [time.newDate(31, 12, time.year(now) - 1)];
  ixV = [0];
  isFirstV = [true];
  while (time.dfDays(Date!, now) <= 0) {
    if (time.weekday(Date!) == 0 | isFirstV!) {
      isFirstV! = false;
      day = time.toStr(Date!);
      while (ixV! < arr.size(InvP) & InvP[ixV!][date] <= day) ixV! += 1;

      pfs = ixV! > 0
        ? InvP[ixV! - 1][profits]
        : arr.size(InvP) > 0
          ? InvP[0][profits]
          : 1.0
      ;

      arr.push(R, profitsEntry.new(day, pfs));
    }

    Date! = time.addDays(Date!, 1);
  }

  return R;
};
