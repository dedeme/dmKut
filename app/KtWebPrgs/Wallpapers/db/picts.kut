// Copyright 26-Sep-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Pictures data base.

import "data/cts";
import "data/pict";
import "data/sighter";

dirV = [""];

// \s -> s
fpath = \group -> return path.cat([dirV!, "picts" + group + ".db"]);;

/// Initialize data base
init = \parentDir -> dirV! = parentDir;;

/// Groups are '0', '1', ..., 'cts.pictureGroups'
/// \ -> [s.]
getGroups = \ -> return arr.fromIter(
    iter.map([0:cts.pictureGroups], \i -> return math.itos(i);)
  );;

/// Update picture data list.
///    group: Group to update.
/// \s -> ()
update = \group -> {
	p = fpath(group);
	if (!file.exists(p)) write(group, []);

  OldPictures = arr.map(js.r(file.read(p)), pict.fromJs);
  NewPictures = []; // [<pict>.]
  saveV = [false];
  for (pictureId = readPictList(group)) {
    pictOp = arr.find(OldPictures, \p -> return p.id == pictureId;);
    if (!!pictOp) {
      arr.push(NewPictures, pictOp!);
    } else {
      arr.push(NewPictures, pict.mk(pictureId));
      saveV! = true;
    }
  }
  if (saveV! | arr.size(NewPictures) != arr.size(OldPictures))
    write(group, NewPictures);
};

// Read pictures identifiers from file path pool.
// \s -> [s.]
readPictList = \group ->
  return arr.filter(
    file.dir(path.cat([cts.pictsPath, group])),
    \name -> return str.ends(name, ".jpg");
  );;

/// Returns pictures ([<pict>.]) of a group serialized with JSON.
/// \s -> s
readJs = \group -> return file.read(fpath(group));;

/// Returns group pictures.
/// \s -> [<pict.>]
read = \group -> return arr.map(js.r(readJs(group)), pict.fromJs);;

/// Write picture.
/// \s, [<pict>.] -> ()
write = \group, pictures ->
  file.write(fpath(group), js.w(arr.map(pictures, pict.toJs)));;

/// Set level of picture 'group'-'id'.
/// \s, s, i -> ()
setLevel = \group, id, level -> {
  Pictures = read(group);
  for (P = Pictures) if (P.id == id) P.level = level;
  write(group, Pictures);
};

/// Returns the next picture
/// \ -> {s, <pict>} :: {group, picture}
next = \ -> {
  GroupS = sighter.next(
    getGroups,
    \group -> return read(group);,
    \group, Sighters -> write(group, Sighters);
  );
  return {group: GroupS.group, picture: GroupS.s};
};

/// Returns total sights and shown sights.
/// \ -> {i, i} :: {totalSights, shownSights}
shownSights = \ -> {
  R = {totalSights: 0, shownSights: 0};
  for (g = getGroups()) for (P = read(g)) {
    R.totalSights += P.level;
    R.shownSights += P.sights;
  }
  return R;
};

