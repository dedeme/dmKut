// Copyright 23-Dic-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Home page.

import "KtWeb/rp";
import "db";
import "art";
import "data/cts";
import "data/image";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      for (f = file.dir(cts.targetPath))
        file.del(path.cat([cts.targetPath, f]));
      ImgsData = db.imagesTb().read();
      ImgsSource = file.dir(cts.sourcePath);
      for (i, im = ImgsSource) {
        pt = path.cat([cts.sourcePath, im]);
        im2 = arr.join(arr.map(
          str.split(im, ""),
          \c -> return (
              (c >= "a" & c <= "z") |
              (c >= "A" & c <= "Z") |
              (c >= "0" & c <= "9") |
              c == "." | c == "@" | c == "_" | c == "+"
            ) ? c : "-";
        ), "");

        if (im != im2) {
          pt2 = path.cat([cts.sourcePath, im2]);
          file.rename(pt, pt2);
          ImgsSource[i] = im2;
        }
      }
      Imgs = image.update(ImgsData, ImgsSource);
      sys.print(rp.mk({
        images: arr.map(Imgs, image.toJs)
      }));
    }
    "idata2": {
      duplicates = db.duplicates(arr.map(
        file.dir(cts.sourcePath), art.changeExt
      ));
      group = db.lessPicturesGroup();
      sys.print(rp.mkJs({
        duplicates: js.wa(arr.map(duplicates, js.ws)),
        group: js.ws(group)
      }));
    }
    "dim": sys.print(rp.mk({
        width: math.itof(db.confTb().read().width),
        height: math.itof(db.confTb().read().height)
      }));
    "save": {
      group = Rq.group;
      db.savePictures(group);
      db.clearIntermediatePaths();
      sys.print(rp.mkEmpty());
    }
    "changeDim": {
      confTb = db.confTb();
      conf = confTb.read();
      conf.width = math.ftoi(Rq.width);
      conf.height = math.ftoi(Rq.height);
      confTb.write(conf);
      sys.print(rp.mkEmpty());
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
