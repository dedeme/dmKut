// Copyright 19-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Companies table

import "db";
import "data/co/cosT";
import "data/co/co";
import "db/quotesDb";

fpath = "cos.tb";

/// Initialize table.
/// \ -> ()
init = \ -> if (!db.exists(fpath))
  db.write(fpath, js.w(cosT.toJs(cosT.new("", []))));;

/// Returns companies in quotes data base.
/// \ -> <cosT>
read = \ -> {
  // [s...]
  Nicks = quotesDb.nicks();

  Changed = [false];
  :cosT cos = cosT.fromJs(js.r(db.read(fpath)));
  // [<co>...]
  NewCos = [];
  for (:co oldC = cos.Cos) {
    if (arr.any(Nicks, \n -> return n == oldC.nick;))
      arr.push(NewCos, oldC);
    else
      Changed! = true;
  }
  for (n = Nicks) {
    if (!arr.any(NewCos, \:co c -> return c.nick == n;)) {
      arr.push(NewCos, co.new(n, false));
      Changed! = true;
    }
  }

  main = arr.any(Nicks, \n -> return n == cos.main;)
    ? cos.main
    : !!Nicks ? Nicks[0] : cos.main
  ;
  if (main != cos.main) Changed! = true;

  R = cosT.new(main, NewCos);
  if (Changed!) db.write(fpath, js.w(cosT.toJs(R)));
  return R;
};

/// Select - Deselect a company.
///   nick: Company nick.
///   value: If it is 'TRUE' 'nick' is selected. Oterwise it is deselected.
/// \s, b -> ()
select = \nick, value -> {
  :cosT cos = read();
  NewCost = arr.map(
    cos.Cos,
    \:co c -> return c.nick == nick ? co.new(nick, value): c;
  );
  db.write(fpath, js.w(cosT.toJs(cosT.new(cos.main, NewCost))));
};

/// Make 'nick' the main company.
/// \s -> ()
setMain = \nick -> {
  :cosT cos = cosT.fromJs(js.r(db.read(fpath)));
  db.write(fpath, js.w(cosT.toJs(cosT.new(nick, cos.Cos))));
};
