// Copyright 19-Jun-2023 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Quotes data base

import "libmkt/qs";
import "libmkt/volumes";
import "libmkt/quotes2";
import "libmkt/cts" : mcts;
import "data/cts";
import "db";

dpath = "quotes";

// \s -> s
fpath = \nick -> return path.cat([dpath, nick + ".tb"]);;

/// Initializes data base
/// \ -> ()
init = \ ->  if (!db.exists(dpath)) file.mkdir(db.pathOf(dpath));;

/// Retrurns registered nicks.
/// \ -> [s...]
nicks = \ -> return arr.map(
    arr.filter(
      file.dir(db.pathOf(dpath)),
      \f -> return str.ends(f, ".tb");
    ),
    \f -> return f[:-3];
  );;

/// Return quotes a company file (from after to before).
/// \s -> s
read = \nick -> return db.read(fpath(nick));;

/// Returns [quotes(<qs>)|error, ok] of a company file (from after to before).
/// \s -> [<qs>|s, b]
readQs = \nick -> return qs.fromStr(mcts.historicQuotes, read(nick));;

/// Write content of a company file.
/// \s, s -> ()
write = \nick, data -> db.write(fpath(nick), data);;

/// Returns [quotes(<quotes>)|error, ok] of a company file (from before to after).
///   mainNick: Nick of the main company. (To extract dates)
///   nick: Nick to make <quotes2>.
/// \s -> [<quotes2>|s, b]
readQuotes = \mainNick, nick -> {
  MainQs = readQs(mainNick);
  if (!MainQs[1]) return MainQs;
  Dates = qs.dates(MainQs!);

  NickQs = readQs(nick);
  if (!NickQs[1]) return NickQs;
  Qs = NickQs!;

  return [
    quotes2.new(
      [nick],
      Dates,
      arr.map(qs.opens(Qs), \Q -> return [Q];),
      arr.map(qs.closes(Qs), \Q -> return [Q];)
    ),
    true
  ];
};

/// Read volume average (in €) of serveral companies.
/// Returns one value for eac company. If data can not be read, the company
/// value is set to 0.
///   Cos    : Nicks of companies to read.
/// [s.] -> [f.]
readVolumes = \Cos -> {
  return volumes.read(db.pathOf(dpath), cts.quotesVolume, Cos);
};
