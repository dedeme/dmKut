// Copyright 12-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Speedometers page.

import "KtWeb/rp";
import "libdm/log";
import "data/cts";
import "data/acc/ann";
import "data/acc/pf";
import "db/acc/diariesDb";
import "db/trading/closesTb";
import "db/trading/refsTb";
import "db/invOperationsTb";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      Closes = closesTb.readOnlyCloses();
      Refs = refsTb.read();

      cashV = [0.0];
      equityV = [0.0];
      Cos = {}; // {[f, f, f]...} :: {nick -> [price, value, ref]}
      for (inv = 0:cts.investors) {
        Ys = diariesDb.investorYears(inv);
        Anns = Ys ? diariesDb.investorAnns(inv, Ys[0]) : [];
        Set = ann.mkSettlement(Anns);
        if (Set.errors) for (e = Set.errors) log.error(e);
        L = Set.ledger;
        Pf = Set.portfolio;

        cashV! += L.cash;
        equityV! += L.equity;
        for (V, nk = Pf) {
          stocks = math.itof(V[0]);
          price = stocks * V[1];

          clOp = dic.get(Closes, nk);
          cl = clOp ? stocks * clOp! : price;

          rfOp = dic.get(Refs, nk);
          rf = rfOp ? stocks * rfOp![inv][1] : price;

          CoOp = dic.get(Cos, nk);
          if (CoOp) {
            Co = CoOp!;
            Cos[nk] = [Co[0] + price, Co[1] + cl, Co[2] + rf];
          } else {
            dic.put(Cos, nk, [price, cl, rf]);
          }
        }
      }
      sys.print(rp.mk({
        cos: Cos,
        cash: cashV!,
        equity: equityV!
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
