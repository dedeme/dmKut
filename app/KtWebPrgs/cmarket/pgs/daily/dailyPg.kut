// Copyright 18-Jul-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Accounting balance page.

import "KtWeb/rp";
import "libdm/log";
import "libmkt/cts" : mkCts;
import "data/cts";
import "data/activity" : actv;
import "data/acc/ann";
import "data/acc/ldg";
import "data/acc/settlement";
import "data/qs/qsvs";
import "data/qs/qsv";
import "data/chart/dailyChart";
import "db/acc/diariesDb";
import "db/daily/dailyChartsTb";
import "db/daily/cosSelTb";
import "db/activityTb";
import "sch";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      CosData = []; // [<dailyChart>...]
      IxsData = []; // [<dailyChart>...]
      for (:dailyChart ch = dailyChartsTb.read()) {
        if (arr.any([cts.meNick] + cts.IxNicks, \n -> return ch.nick == n;))
          arr.push(IxsData, ch);
        else arr.push(CosData, ch);
      }
      arr.sort(IxsData, \:dailyChart d1, :dailyChart d2 -> return
        d1.nick == cts.meNick
          ? true
          : d1.nick == cts.ibexNick
              ? d2.nick != cts.meNick
              : false
      ;);

      DicRebuys = {}; // {s.} ;: {nick -> date}
      Ys = diariesDb.investorYears();
      for (y = Ys[:2]) ann.rebuys(DicRebuys, diariesDb.investorAnns(y));

      serverName = qsv.name(qsvs.currentDiary());
      activity = activityTb.read()[actv.activity];

      Anns = !!Ys ? diariesDb.investorAnns(Ys[0]) : [];
      :settlement set = ann.mkSettlement(Anns);
      maxCosV = [20];
      if (!!set.Errors) {
        for (e = set.Errors) log.error(e);
      } else {
        cash = set.ledger[ldg.cash] - mkCts.minToBet + mkCts.bet;
        maxCosV! = math.ftoi(cash) / math.ftoi(mkCts.bet);
      }

      sys.print(rp.mk({
        CosData: arr.map(CosData, dailyChart.toJs),
        IxsData: arr.map(IxsData, dailyChart.toJs),
        CosSel: cosSelTb.read(),
        Rebuys: dic.keys(DicRebuys),
        serverName,
        activity,
        maxCos: maxCosV!
      }));
    }
    "reactivate": {
      sch.dailyActivate();
      sys.print(rp.mkEmpty());
    }
    "setSelected": {
      nick = Rq.nick;
      isSel = Rq.isSel;
      CosSel = cosSelTb.read();
      if (isSel) arr.push(CosSel, nick);
      else arr.filterIn(CosSel, \n -> return n != nick;);

      cosSelTb.write(CosSel);
      sys.print(rp.mkEmpty());
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
