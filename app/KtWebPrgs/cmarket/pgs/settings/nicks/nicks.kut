// Copyright 19-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Nicks page

import "KtWeb/rp";
import "libmkt/qs";
import "libdm/log";
import "db/quotesDb";
import "db/cosTb";
import "data/co/cosT";
import "data/co/co";
import "data/cts";
import "qspr";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      :cosT cos = cosTb.read();
      Cos = arr.map(cos.Cos, \:co c -> return c.nick;);
      // {f...}
      Volumes = {};
      Vols = quotesDb.readVolumes(Cos);
      for (Tp = arr.zip([Cos, Vols]))
        dic.put(Volumes, Tp[0], Tp[1]);

      sys.print(rp.mk({
        main: cos.main,
        cos: arr.map(cos.Cos, co.toJs),
        vol: Volumes
      }));
    }
    "setMain": {
      cosTb.setMain(Rq.nick);
      sys.print(rp.mkEmpty());
    }
    "select": {
      cosTb.select(Rq.nick, Rq.value);
      if (Rq.value) qspr.updateClosesAndRefs(Rq.nick);
      sys.print(rp.mkEmpty());
    }
    "test": {
      nk = Rq.nick;
      WithErrors = [false];
      WithWarnings = [false];
      // [<qs>|s, b]
      Qs = quotesDb.readQs(nk);
      if (Qs[1]) {
        QQs = [Qs!];
        // [<qs>, [s...]]
        Tp = qs.correct(QQs!);
        QQs! = Tp[0];
        // [s...]
        Errs = Tp[1];
        Mqs = quotesDb.readQs(cosTb.read()[cosT.main]);
        if (Mqs[1]) {
          // [<qs>, [s...]]
          Tp2 = qs.correctDates(QQs!, Mqs!);
          for (err = Tp2[1]) arr.push(Errs, err);
        }
        if (!!Errs){
          WithWarnings! = true;
          log.error(str.fmt(
            "Warnings testing %v (%v and %v more)",
            [nk, Errs[0], arr.size(Errs) - 1]
          ));
        }
      } else {
        WithErrors! = true;
        log.error(str.fmt("Error testing %v (%v)", [nk, Qs!]));
      }

      sys.print(rp.mk({
        withErrors: WithErrors!,
        withWarnings: WithWarnings!
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};


