// Copyright 18-Jul-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Accounting balance page.

import "KtWeb/rp";
import "libdm/log";
import "data/cts";
import "data/acc/ann";
import "data/acc/settlement";
import "data/acc/ldg";
import "db/acc/diariesDb";

// \s -> {b, [[s, f, f].], f} :: {ok, entries, cash}
cMarketData = \lastDate -> {
  okV = [true];
  year = lastDate[:4];
  cashV = [0.0];

  Anns = arr.filter(
    diariesDb.investorAnns(year),
    \:ann a -> return a.date <= lastDate;
  );
  :settlement set = ann.mkSettlement(Anns);
  if (!!set.Errors) {
    okV! = false;
    for (e = set.Errors) log.error(e);
  }
  cashV! += set.ledger[ldg.cash];
  Pf = set.Portfolio;

  Entries = arr.map(
    dic.toArr(Pf),
    \Kv -> return [Kv[0], Kv[1][0], Kv[1][1]];
  );
  return {
    ok: okV!,
    entries: Entries,
    cash: cashV!
  };
};

// \ -> {[[s, f, f, f].], f} :: {entries, cash}
stocksData = \ -> {
  All = js.r(file.read("/dm/KtWeb/dmcgi/Stocks/all.tb"));
  y = time.fmt("%Y", time.now());
  Year = arr.find(All, \E -> return E[0] == y;)!;
  Anns = Year[1][1];
  Pf = {}; //{[f, f].} :: {nick -> [stocks, price]}
  for (A = Anns) {
    nick = A[3];
    stocks = A[4];
    price = A[5];
    if (A[1]) { // isSell
      E = Pf[nick];
      if (E[0] < stocks)
        sys.throw(str.fmt(
          "Selling %v stocks of %v when only there are %v",
          [stocks, nick, E[0]]
        ));
      else E[0] -= stocks;
    } else {
      EOp = dic.get(Pf, nick);
      if (!!EOp) {
        E = EOp!;
        sum = E[0] + stocks;
        E[1] = (E[0] * E[1] + stocks * price) / sum;
        E[0] = sum;
      } else {
        E = [0.0, 0.0];
        E[0] = stocks;
        E[1] = price;
        dic.put(Pf, nick, E);
      }
    }
  }
  Entries = arr.map(
    arr.filter(
      dic.toArr(Pf),
      \Kv -> return Kv[1][0] > 0.0;
    ),
    \Kv -> {
      stocks = Kv[1][0];
      ttCost = stocks * Kv[1][1];
      return [Kv[0], stocks, ttCost / stocks, ttCost];
    }
  );
  return {
    entries: Entries,
    cash: arr.reduce(Entries, 0.0, \r, E -> return r + E[3];)
  };
};

// \ -> {f, f} :: {cash, stocks}
hcontaData = \ -> {
  accCash = "57202";
  accStocks = "54000";
  cashV = [0.0];
  stocksV = [0.0];
  y = time.fmt("%Y", time.now());
  Data = js.r(file.read("/dm/KtWeb/dmcgi/Hconta/" + y + ".db"));
  Anns = Data[3];
  for (A = Anns) {
    Ds = A[2];
    Cs = A[3];

    amCashDOp = dic.get(Ds, accCash);
    if (!!amCashDOp) cashV! += amCashDOp!;
    else {
      amCashCOp = dic.get(Cs, accCash);
      if (!!amCashCOp) cashV! -= amCashCOp!;
    }

    amStocksDOp = dic.get(Ds, accStocks);
    if (!!amStocksDOp) stocksV! += amStocksDOp!;
    else {
      amStocksCOp = dic.get(Cs, accStocks);
      if (!!amStocksCOp) stocksV! -= amStocksCOp!;
    }
  }
  return {cash: cashV!, stocks: stocksV!};
};

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      lastDate = Rq.lastDate;

      CMarketD = cMarketData(lastDate);
      ok = CMarketD.ok;

      StocksD = stocksData();

      HcontaD = hcontaData();

      sys.print(rp.mk({
        ok,
        cMarketCash: CMarketD.cash,
        cMarketStocks: CMarketD.entries,
        stocksSum: StocksD.cash,
        stocksStocks: StocksD.entries,
        hcontaCash: HcontaD.cash,
        hcontaSum: HcontaD.stocks
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
