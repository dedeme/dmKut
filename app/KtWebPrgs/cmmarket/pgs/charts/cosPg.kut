// Copyright 27-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Company charts page.

import "KtWeb/rp";
import "libmkt/models";
import "libmkt/quotes2";
import "libmkt/order";
import "libmkt/strategy";
import "libmkt/stRs";
import "data/result";
import "data/modelEval";
import "data/jsModel";
import "data/cts";
import "db";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      mdId = Rq.modelId;
      PParams = Rq.params; // [[f...]]|[]
      MMd = models.get(mdId);
      if (!MMd) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      :model md = MMd!;

      Evals = db.readEvals(mdId).evals;
      EEval = !!PParams
        ? modelEval.get(Evals, PParams!)
        : [modelEval.best(Evals)]
      ;
      if (!EEval) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      Eval = EEval!;
      :quotes2 qts = db.readQuotes();
      :stRs StrgRs = strategy.open(md, qts, Eval.params);

      sys.print(rp.mk({
        ok: true,
        model: jsModel.toJs(models.get(mdId)!),
        result: result.toJs(result.new(
            arr.peek(StrgRs.Hassets),
            arr.reduce(StrgRs.Profits, 0.0, \r, e -> return r + e;) /
              math.itof(arr.size(StrgRs.Profits)),
            math.itof(order.sales(StrgRs.Orders))
          )),
        eval: modelEval.toJs(Eval),
        cos: qts.Cos
      }));
    }
    "co": {
      mdId = Rq.modelId;
      Params = Rq.params;
      co = Rq.co;

      md = models.get(mdId)!;
      fqts = db.readQuotes();
      coIx = quotes2.companyIndex(fqts, co);
      if (coIx == -1)
        sys.throw("Company " + co + " not found");

      :quotes2 qts = quotes2.getSingle(fqts, coIx);
      :stRs StrgRs = strategy.open(md, qts, Params);
      Result = result.new(
        arr.peek(StrgRs.Hassets),
        arr.reduce(StrgRs.Profits, 0.0, \r, e -> return r + e;) /
          math.itof(arr.size(StrgRs.Profits)),
        math.itof(order.sales(StrgRs.Orders))
      );
      sys.print(rp.mk({
        result: result.toJs(Result),
        dates: qts.Dates,
        qs: arr.map(qts.Closes, \Row -> return Row[0];),
        refs: arr.map(StrgRs.Refs, \Row -> return Row[0];)
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
