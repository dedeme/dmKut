// Copyright 27-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Company charts page.

import "KtWeb/rp";
import "libmkt/ffi";
import "libmkt/model";
import "libmkt/models";
import "libmkt/quotes";
import "libmkt/order";
import "libmkt/strategy";
import "data/result";
import "data/modelEval";
import "data/cts";
import "db";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      mdId = Rq.modelId;
      PParams = Rq.params; // [[f...]]|[]
      MMd = models.get(mdId);
      if (!MMd) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      md = MMd!;

      Evals = db.readEvals(mdId).evals;
      EEval = PParams
        ? modelEval.get(Evals, PParams!)
        : [modelEval.best(Evals)]
      ;
      if (!EEval) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      Eval = EEval!;
      AllQs = db.readQuotes();
      StrRs = strategy.run(
        md, AllQs, Eval.params, cts.daysWin, cts.daysLoss
      );

      Dates = AllQs.dates;
      dSize = arr.size(Dates);
      noLossM = ffi.noLossMultiplicator();
      Pf = {}; // {f.} :: {nick -> price}
      BuyCos = arr.new(dSize, 0);
      QuarantineCos = arr.new(dSize, 0);
      Orders = StrRs.orders;
      oSize = arr.size(Orders);
      if (Orders) {
        iOV = [0];
        OV = [Orders[iOV!]];
        oDateV = [OV!.date];
        for (i, d = Dates) {
          while(oDateV! <= d & iOV! < oSize) {
            o = OV!;
            if (o.type == order.buy) {
              dic.put(Pf, o.nick, o.price);
            } else {
              bprice = Pf[o.nick];
              dic.remove(Pf, o.nick);
              days = o.price >= bprice * noLossM ? cts.daysWin : cts.daysLoss;
              end0 = i + days;
              end = end0 >= dSize ? dSize : end0;
              for (j = i:end) QuarantineCos[j] += 1;
            }
            iOV! += 1;
            if (iOV! < oSize) {
              OV! = Orders[iOV!];
              oDateV! = OV!.date;
            }
          }
          BuyCos[i] = dic.size(Pf);
        }
      }

      sys.print(rp.mk({
        ok: true,
        model: model.toJs(models.get(mdId)!),
        result: result.toJs(result.new(
            StrRs.hassets[arr.size(StrRs.hassets)-1],
            arr.reduce(StrRs.profits, 0.0, \r, e -> return r + e;) /
              math.itof(arr.size(StrRs.profits)),
            math.itof(order.sales(StrRs.orders))
          )),
        eval: modelEval.toJs(Eval),
        dates: Dates,
        assets: StrRs.hassets,
        withdrawals: StrRs.hwithdrawals,
        buyCos: arr.map(BuyCos, math.itof),
        quarantineCos: arr.map(QuarantineCos, math.itof)
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
