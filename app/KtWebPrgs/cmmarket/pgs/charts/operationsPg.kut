// Copyright 29-Jun-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Company charts page.

import "KtWeb/rp";
import "libmkt/models";
import "libmkt/quotes2";
import "libmkt/order";
import "libmkt/strategy";
import "libmkt/stRs";
import "data/result";
import "data/modelEval";
import "data/jsModel";
import "data/cts";
import "db";

/// \m -> ()
process = \Rq -> {
  switch (Rq.rq) {
    "idata": {
      mdId = Rq.modelId;
      PParams = Rq.params; // [[f...]]|[]
      MMd = models.get(mdId);
      if (!MMd) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      md = MMd!;

      Evals = db.readEvals(mdId).evals;
      EEval = !!PParams
        ? modelEval.get(Evals, PParams!)
        : [modelEval.best(Evals)]
      ;
      if (!EEval) {
        sys.print(rp.mk({ok: false}));
        return;
      }
      Eval = EEval!;
      :quotes2 qts = db.readQuotes();
      :stRs StrgRs = strategy.open(md, qts, Eval.params);

      sys.print(rp.mk({
        ok: true,
        model: jsModel.toJs(models.get(mdId)!),
        result: result.toJs(result.new(
            arr.peek(StrgRs.Hassets),
            arr.reduce(StrgRs.Profits, 0.0, \r, e -> return r + e;) /
              math.itof(arr.size(StrgRs.Profits)),
            math.itof(order.sales(StrgRs.Orders))
          )),
        eval: modelEval.toJs(Eval),
        nicks: qts.Cos,
        lastCloses: qts.Closes[arr.size(qts.Closes) - 1],
        orders: arr.map(StrgRs.Orders, \O -> return order.toJs(O);)
      }));
    }
    default: sys.throw("Value of rq (" + Rq.rq + ") is not valid");
  }
};
