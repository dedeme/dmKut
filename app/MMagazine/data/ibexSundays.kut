// Copyright 27-Mar-2022 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Sunday data of ibex.

/// Creates a new <IbexSundays>
///   Data: Array of pairs date-value. It can be empty.
/// \[[i, f]...] -> m<IbexSundays>
new = \data -> return {
  data
};;

/// \<IbexSundays> -> [i...]
dates = \Is -> return arr.mp(Is.data, \e -> return e[0];);;

/// \<IbexSundays> -> [f...]
values = \Is -> return arr.mp(Is.data, \e -> return e[1];);;

/// Returns values of 'Is' divided by 'Is[0]'
/// \<IbexSundays> -> [f...]
ratios = \Is -> {
  D = Is.data;
  if (arr.size(D) == 0) return [];
  base = D[0][1];
  return arr.mp(D, \e -> return (e[1] - base) / base;);
};

/// Adds a new entry if there is no another with the same date.
/// \<IbexSundays>, i, f -> ()
add = \Is, date, value -> {
  day = time.toStr(date);
  D = Is.data;
  if (!arr.any(D, \e -> return time.toStr(e[0]) == day;))
    arr.push(D, [date, value]);
};

/// \<IbexSundays> -> s
toJs = \Is ->
  return js.wa(arr.mp(
    Is.data,
    \e -> return js.wa([
        js.ws(time.toStr(e[0])),
        js.ws(str.fmt("%.2f", [e[1]]))
      ]);
  ));;

/// \s -> <IbexSundays>
fromJs = \j ->
  return new(arr.mp(
    js.ra(j),
    \e -> {
      tp = js.ra(e);
      return [time.fromStr(js.rs(tp[0])), math.toFloat(js.rs(tp[1]))];
    }
  ));;
