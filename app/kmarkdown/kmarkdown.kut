// Copyright 11-Oct-2023 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Program main file.

import "book";
import "meta";
import "htmlHeader";
import "styles";
import "err";
import "metadata";
import "reader";
import "writer";

/// Write 'index.html' page.
///   dir   : Web root directory.
///   header: Text of file '000header.html'
///   mBook  : Page metadata. See 'metadata'.
/// \s,s,<book> -> ()
writeIndex = \dir, header, :book mBook -> {
  fhtml = path.cat([dir, "index.html"]);
  head = str.fmt("""
    <table class="kmd-main"><tr><td style="text-align:left">
      <span class="kmd-title">%s</span>
      </td><td style="text-align:right">
      <span class="kmd-title">Índice</span>
    </td></tr></table>
    <hr>
    """,
    [mBook.name]
  );
  index = arr.join(arr.map(mBook.Pages, \:meta p ->
    return "<a href='" + path.base(p.fpage) + ".html'>" + p.title + "</a>";
  ), "<br>\n");

  file.write(fhtml,
    "<!doctype html>\n<html>\n" +
    header +
    "\n<body>\n" +
    head +
    "<h2>Índice</h2>\n<p style='text-indent:0px'>\n" +
    index +
    "\n</p>\n</body>\n</html>\n"
  );
};

/// Processes a file.
///   header: Path of HTML file '000header.html'.
///   mBook : Page metadata. See 'metadata'.
///   fkmd  : Path fo '.kmd' file.
/// \s, <book>, s -> ()
processFile = \header, :book mBook, fkmd -> {
  fkmdName = fkmd[:-4];
  pageMetaOp = metadata.getCurrent(mBook.Pages, fkmdName);
  if (!pageMetaOp) sys.throw("Meta page not found");
  :meta pageMeta = pageMetaOp!;

  prevMetaOp = metadata.getPrevious(mBook.Pages, fkmdName);
  nextMetaOp = metadata.getNext(mBook.Pages, fkmdName);
  numbered = pageMeta.numbered;
  withUp = pageMeta.withUp;
  withDown = pageMeta.withDown;

  head = mBook.name == ""
    ? ""
    : str.fmt("""
        <table class="kmd-main"><tr><td style="text-align:left">
          <span class="kmd-title">%s</span>
          </td><td style="text-align:right">
          <span class="kmd-title">%s</span>
        </td></tr></table>
        <hr>
        <p style="text-indent: 0px">%s<a href="index.html">Índice</a>%s</p>
        <hr>
        """,
        [ pageMeta.title,
          mBook.name,
          !prevMetaOp
            ? ""
            : "<a href='" +
              path.base(prevMetaOp![meta.fpage]) +
              ".html'>Anterior (" +
              prevMetaOp![meta.title] +
              ")</a> · ",
          !nextMetaOp
            ? ""
            : " · <a href='" +
              path.base(nextMetaOp![meta.fpage]) +
              ".html'>Posterior (" +
              nextMetaOp![meta.title] +
              ")</a>"
        ]
      )
  ;

  footer = mBook.name == ""
    ? ""
    : str.fmt("""
        <hr>
        <p style="text-indent: 0px">%s<a href="index.html">Índice</a>%s</p>
        """,
        [ !prevMetaOp
            ? ""
            : "<a href='" +
              path.base(prevMetaOp![meta.fpage]) +
              ".html'>Anterior (" +
              prevMetaOp![meta.title] +
              ")</a> · ",
          !nextMetaOp
            ? ""
            : " · <a href='" +
              path.base(nextMetaOp![meta.fpage]) +
              ".html'>Posterior (" +
              nextMetaOp![meta.title] +
              ")</a>"
        ]
      )
  ;

  :arr Hs = [];
  rd = reader.new(fkmd);
  err.setReader(rd);
  body = writer.process(Hs, numbered, rd);
  index = !Hs
    ? ""
    : str.fmt("""
        <p style='text-indent:0px'>
        %v
        </p>
        <hr>
        """,
        [ arr.join(Hs.map(\h ->
            return "<a href='#" + h + "'>" +
              h + "</a>";
          ), "<br>\n")
        ]
      )
  ;
  fhtml = fkmd[:-3] + "html";
  file.write(fhtml,
    "<!doctype html>\n<html>\n" +
    header +
    "\n<body>\n" +
    (withUp | withDown
      ? "<div id='hp:START_PAGE'></div>\n"
      : ""
    ) +
    head +
    index +
    body +
    footer +
    (withUp
      ? """
        <div style="position: fixed;bottom: 0px;right: 10px;cursor: pointer;">
          <span onclick="location.assign('#hp:START_PAGE');">
            <img border="0" src="img/up.png" alt="" />
          </span>
        </div>
        """
      : ""
    ) +
    (withDown
      ? """
        <div style="position: fixed;top: 0px;right: 10px;cursor: pointer;">
          <span onclick="location.assign('#hp:END_PAGE');">
            <img border="0" src="img/down.png" alt="" />
          </span>
        </div>
        """
      : ""
    ) +
    (withUp | withDown
      ? "<div id='hp:END_PAGE'></div>\n"
      : ""
    ) +
    "\n</body>\n</html>\n"
  );
};

/// Processes a directory.
///   dir: Web directory.
/// \s -> ()
processDir = \dir -> {
  :arr Fkmds = [];
  for (f = file.dir(dir)) {
    :file fpath = path.cat([dir, f]);
    if (fpath.isDirectory()) {
      processDir(fpath);
    } else if (str.ends(f, ".kmd")) {
      Fkmds.push(fpath);
    }
  }

  if (!Fkmds) return ;

  fstyles = path.cat([dir, "styles.css"]);
  if (!file.exists(fstyles)) file.write(fstyles, styles.text);

  fheader = path.cat([dir, "000header.html"]);
  if (!file.exists(fheader)) file.write(fheader, htmlHeader.text);
  header = file.read(fheader);

  :book mBook = metadata.read(Fkmds);

  if (mBook.name != "") writeIndex(dir, header, mBook);

  for (fkmd = Fkmds) processFile(header, mBook, fkmd);
};

processDir("./");
