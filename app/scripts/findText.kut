// Copyright 12-Jan-2023 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

// Use kut finText path ext text
// Finds a 'text' in every text file with extension .'ext' in 'path'

Args = sys.args();

if (arr.size(Args) != 3) {
  sys.println("""
    Use:
      kut findText path ext text
    For example:
      kut findText /dm/dmKut kut ext.wget"""
  );
  return;
}

p = Args[0];
lenP1 = str.len(p) + 1;
ext = "." + Args[1];
text = Args[2];

if (!file.isDirectory(p)) {
  sys.println("'" + p + "' is not a directory");
  return;
}

searchFile = \fpath, msgs -> {
  if (!str.ends(fpath, ext)) return;
  f = file.ropen(fpath);
  fcut = fpath[lenP1:];
  NLine = [1];
  L = [file.readLine(f)];
  while (!arr.empty(L!)) {
    if (str.index(L!!, text) != -1) {
      arr.push(msgs, str.fmt("%v:%v:\n  %v", [
        fcut, NLine!, str.trim(L!!)
      ]));
    }
    NLine! += 1;
    L! = file.readLine(f);
  }
  file.close(f);
};

searchDir = \p, msgs -> {
  for (f = file.dir(p)) {
    fpath = path.cat([p, f]);
    if (file.isDirectory(fpath)) searchDir(fpath, msgs);
    else searchFile(fpath, msgs);
  }
};

sys.println("Results:");
msgs = [];
searchDir(p, msgs);
for (msg = msgs) {
  sys.println(msg);
}

