// Copyright 09-Mar-2024 ÂºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Generator of secodary words for geany.

// \ -> s
help = \ -> return """
  Use kgeanyWords projectRoot
  """;;

:file kutConfPath = "/home/deme/.config/geany/filedefs/filetypes.Kut.conf";
:file jkutConfPath = "/home/deme/.config/geany/filedefs/filetypes.Jkut.conf";
:file nkutConfPath = "/home/deme/.config/geany/filedefs/filetypes.Nkut.conf";
:file kuttConfPath = "/home/deme/.config/geany/filedefs/filetypes.Kutt.conf";

sys.assert(kutConfPath.exists());
sys.assert(jkutConfPath.exists());
sys.assert(nkutConfPath.exists());
sys.assert(kuttConfPath.exists());

:arr Args = sys.args();
if (Args.size() != 1) {
  sys.println(help());
  return;
}

:file pRoot = Args[0];
if (!pRoot.isDirectory()) {
  sys.printError(pRoot + " not found");
  return;
}

:arr Kuts = str.split(
  "arr b64 bytes cryp dic file iter js math regex str sys tcp thread time",
  " "
);
:arr Jkuts = str.split(
  "arr b64 bytes client cryp dic domo iter js math storage str sys time timer ui",
  " "
);
:arr Nkuts = str.split(
  "arr b64 bytes cryp dic file iter js math str sys thread time timer",
  " "
);
:arr Kutts = str.split(
  "arr b64 bytes cryp dic file iter js math regex str sys tcp thread time",
  " "
);

// \s -> b
isValidWord = \f -> {
  isValidFirst = \ch -> return (ch >= 'a' & ch <= 'z') | (ch >= 'A' & ch <= 'Z');;
  isValidCont = \ch -> return isValidFirst(ch) | (ch >= '0' & ch <= '9');;

  if (!isValidFirst(f[0])) return false;
  for (i = 1:str.len(f)) if (!isValidCont(f[i])) return false;
  return true;
};

populate = \:file d -> {
  for (f = d.dir()) {
    :file fpath = file.cat([d, f]);
    if (fpath.isDirectory())
      populate(fpath);
    else if (str.ends(fpath, ".kut") & isValidWord(f[:-4]))
      Kuts.push(f[:-4]);
    else if (str.ends(fpath, ".jkut") & isValidWord(f[:-5]))
      Jkuts.push(f[:-5]);
    else if (str.ends(fpath, ".nkut") & isValidWord(f[:-5]))
      Nkuts.push(f[:-5]);
    else if (str.ends(fpath, ".ktt") & isValidWord(f[:-4]))
      Kutts.push(f[:-4]);
  }
};
populate(pRoot);

Kuts.sort(\e1, e2 -> return e1 < e2;);
Jkuts.sort(\e1, e2 -> return e1 < e2;);
Nkuts.sort(\e1, e2 -> return e1 < e2;);
Kutts.sort(\e1, e2 -> return e1 < e2;);

// \s, [s.] -> ()
mkConf = \:file kPath, :arr Kuts ->
  kPath.write(arr.join(
    arr.map(
      str.splitTrim(kPath.read(), "\n"),
      \:str l -> return l.starts("secondary=")
          ? "secondary=" + Kuts.join(" ")
          : l
        ;
    ), "\n"
  ));;


mkConf(kutConfPath, Kuts);
mkConf(jkutConfPath, Jkuts);
mkConf(nkutConfPath, Nkuts);
mkConf(kuttConfPath, Kutts);


