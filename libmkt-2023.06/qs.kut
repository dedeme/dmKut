// Copyright 14-Jun-2023 ºDeme
// GNU General Public License - V3 <http://www.gnu.org/licenses/>

/// Array of quotes management.

import "quote";

/// Returns a [quotes|error, ok] with quotes from the content of a
/// 'NICK.tb' file. (From after to before)
/// If the final number of quotes is different to 'size', or
/// some quote can not be read, the first error found is returned.
///   size: Number of quotes to return.
///   s   : Text from a 'NICK.tb' file.
/// \i, s -> [<qs>|s, b]
fromStr = \size, s -> {
  s = str.trim(s);
  if (!s) return [false, "Quotes string is empty"];

  // [<quote>...]
  Qs = [];
  Err = [""];
  try {
    for (l = str.split(s, "\n")) arr.push(Qs, quote.fromStr(l));
  } catch (e) {
    Err! = e;
  }

  if (Err!) return [Err!, false];

  if (arr.size(Qs) != size) {
    return [
      str.fmt("Wrong quotes number. Expected %d. Found %d", [size, arr.size(Qs)]),
      false
    ];
  }

  return [Qs, true];
};

/// Returns text to write in a 'NICK.tb' file.
/// \<qs> -> s
toStr = \qts -> return arr.join(arr.map(qts, quote.toStr), "\n");;

/// Returns the quotes number of 'qts'
/// \<qs> -> i
size = \qts -> return arr.size(qts);;

/// Corrects "qts" and returns a list with corrections.
///   qts: Historic quotes.
///   ----
///   Returns [quotes, errors]:
///     A[0] : Quotes, corrected if necessary.
///     A[1] : Array with corrections.
/// \<qs> -> [<qs>, [s...]]
correct = \qts -> {
  // [s...]
  Errs = [];

  // \<quote>, [s...] -> ()
  appendErrs = \Q, NewErrs ->
    for (e = NewErrs) arr.push(Errs, str.fmt("%v: %v", [Q.date, e]));;

  if (!qts) return [[], []];

  // <quote>
  NewQs = [];
  Q1 = [[]];
  Q2 = [qts[0]];
  for (i = 1:arr.size(qts)) {
    Q1! = Q2!;
    Q2! = qts[i];

    // [<quote>, [s...]]
    Rs0 = quote.correct2(Q1!, Q2!);
    appendErrs(Rs0[0], Rs0[1]);

    // [<quote>, [s...]]
    Rs1 = quote.correct2(Rs0[0], Q2!);
    appendErrs(Rs1[0], Rs1[1]);

    arr.push(NewQs, Rs1[0]);
  }
  // [<quote>, [s...]]
  Rs = quote.correct1(Q2!);
  arr.push(NewQs, Rs[0]);
  appendErrs(Rs[0], Rs[1]);

  return [NewQs, Errs];
};

/// Corrects extra o missing quotes in "qts" and returns a list with corrected
/// quotes.
///   mqts: Historic quotes of main company.
///   qts : Historic quotes of company.
///   ----
///   Returns [quotes, errors]:
///     A[0] : Quotes, corrected if necessary.
///     A[1] : Array with corrections.
/// \<qs>, <qs> -> [<qs>, [s...]]
correctDates = \mqts, qts -> {
  Errs = [];

  // \<quote>, s -> ()
  appendErr = \Q, err -> arr.push(Errs, str.fmt("%v: %v", [Q.date, err]));;

  // <qs>
  NewQs = [];
  MI = [0];
  mSz= arr.size(mqts);
  I = [0];
  sz = arr.size(qts);
  while () {
    mI = MI!;
    i = I!;

    if (mI < mSz) {
      MQ = mqts[mI];
      if (i < sz) {
        Q = qts[i];
        if (MQ.date > Q.date) {
          appendErr(MQ, "Missing quote");
          arr.push(NewQs, quote.newMissing(MQ.date));
          MI! += 1;
          continue;
        }
        if (MQ.date < Q.date) {
          appendErr(Q, "Extra quote");
          I! += 1;
          continue;
        }
        arr.push(NewQs, Q);
        I! += 1;
        MI! += 1;
        continue;
      }
      appendErr(MQ, "Missing quote");
      arr.push(NewQs, quote.newMissing(MQ.date));
      MI! += 1;
      continue;
    }

    if (i < sz) {
      appendErr(qts[i], "Extra quote");
      I! += 1;
      continue;
    }
    break;
  }

  return [NewQs, Errs];
};

/// Merges new quotes with others already existent.
///   mqts  : Main company quotes. When quotes are of main company, it is an
///           empty array.
///   newQts: Last quotes read from the Internet. It can not be empty.
///   qts   : Existent quotes in file system.
///   ----
///   Returns [quotes, errors]:
///     A[0]: Quotes made with the following process:
///           1. Every quote on top with 'open = -1' is removed from "this"
///              in the range of "newQts" dates.
///           2. If there are new and old quotes for the same date, the old
///              is selected.
///           3. The return array is corrected.
///     A[1]: Array with Errors returned by 'correct' and 'correctDates' in format
///           "date: error".
/// \<qs>, <qs>, <qs> -> [<qs>, [s...]]
merge = \mqts, newQts, qts -> {
  I = [0];
  sz = arr.size(qts);
  Qs = arr.copy(qts); // to modify
  NqtsI = [0];
  nqtsSz = arr.size(newQts);
  QtsI = [0];

  while () {
    i = I!;
    nqtsI = NqtsI!;
    qtsI = QtsI!;

    if (i >= sz) break;

    if (nqtsI < nqtsSz) {
      NQ = newQts[nqtsI];
      if (qtsI < sz) {
        OldQ = qts[qtsI];
        if (NQ.date > OldQ.date) {
          Qs[i] = NQ;
          I! += 1;
          NqtsI! += 1;
          continue;
        }
        if (NQ.date < OldQ.date) {
          Qs[i] = OldQ;
          I! += 1;
          QtsI! += 1;
          continue;
        }
        if (quote.isMissing(OldQ)) Qs[i] = NQ;
        else Qs[i] = OldQ;
        I! += 1;
        QtsI! += 1;
        NqtsI! += 1;
        continue;
      }
    }

    if (qtsI >= sz) {
      Qs[i] = qts[qtsI];
      I! += 1;
      QtsI! += 1;
      continue;
    }

    break;
  }

  QsR = [Qs];
  // [s...]
  Errs = [];

  if (mqts) {
    // [<qs>, [s...]]
    Rs = correctDates(mqts, QsR!);
    QsR! = Rs[0];
    for (err = Rs[1]) arr.push(Errs, err);
  }

  // [<qs>, [s...]]
  Rs = correct(QsR!);
  QsR! = Rs[0];
  for (err = Rs[1]) arr.push(Errs, err);

  return [QsR!, Errs];
};

/// Returns an [<quote...], copy of this, ordered from before to after.
///   qts: Historic quotes.
/// \<qs> -> [<quote>...]
quotes = \qts -> return arr.reverse(qts);;

/// Returns a array of dates with 'size == qs_size(this)', ordered from
/// before to after
///   qts: Historic quotes.
/// \<qs> -> [s...]
dates = \qts -> return arr.map(arr.reverse(qts), \Q -> return Q.date;);;

// \[f...] -> ()
normalize = \Vs -> {
  Start = [-1.0];
  for (n, i = Vs)
    if (n >= 0) {
      Start! = n;
      break;
    }

  if (Start! < 0) sys.throw("Every quote is < 0");

  Vs[0] = Start!;
  for (i = 1:arr.size(Vs))
    if (Vs[i] < 0) Vs[i] = Vs[i - 1];
};

/// Returns a array of opens with 'size = qs_size(this)', normalized and
/// ordered from before to after
///   qts: Historic quotes.
/// \<qs> -> [f...]
opens = \qts -> {
  R = arr.map(arr.reverse(qts), \Q -> return Q.open;);
  normalize(R);
  return R;
};

/// Returns a array of closes with 'size = qs_size(this)', normalized and
/// ordered from before to after
///   qts: Historic quotes.
/// \<qs> -> [f...]
closes = \qts -> {
  R = arr.map(arr.reverse(qts), \Q -> return Q.close;);
  normalize(R);
  return R;
};

/// Returns a array of maxs with 'size = qs_size(this)', normalized and
/// ordered from before to after
///   qts: Historic quotes.
/// \<qs> -> [f...]
maxs = \qts -> {
  R = arr.map(arr.reverse(qts), \Q -> return Q.max;);
  normalize(R);
  return R;
};

/// Returns a array of mins with 'size = qs_size(this)', normalized and
/// ordered from before to after
///   this: Historic quotes.
/// \<qs> -> [f...]
mins = \qts -> {
  R = arr.map(arr.reverse(qts), \Q -> return Q.min;);
  normalize(R);
  return R;
};

/// Returns the volume average (in €) of a company quotes.
///   qts    : Historic quotes. (From after to before).
///   samples: Number of quotes to take in account.
/// \s, i -> f
volumeAvg = \qts, samples -> {
  Entries = str.split(qts, "\n");

  Maxs = [0.0, 0.0, 0.0];
  Mins = [0.0, 0.0, 0.0];
  Count0 = [0];
  Count = [0];
  Sum = [0.0];
  for (Entry = Entries) {
    Ps = str.splitTrim(Entry, ":");
    if (arr.size(Ps) != 7) continue;
    Max = math.stof(Ps[3]);
    Min = math.stof(Ps[4]);
    Vol = math.stoi(Ps[5]);

    if (!Max | !Min | !Vol | Vol! < 0) continue;

    v = (Max! + Min!) * math.itof(Vol!);
    if (Count0! < 3) {
      i = Count0!;
      Maxs[i] = v;
      Mins[i] = v;
      Count0! += 1;
      continue;
    }

    V = [v];
    More = [true];
    for (i = 0:3)
      if (V! > Maxs[i]) {
        tmp = V!;
        V! = Maxs[i];
        Maxs[i] = tmp;
        More! = false;
      }
    if (More!)
      for (i = 0:3)
        if (V! < Mins[i]) {
          tmp = V!;
          V! = Mins[i];
          Mins[i] = tmp;
        }

    Sum! += V!;
    Count! += 1;
    if (Count! == samples) break;
  }
  if (Count! < samples)
    sys.throw(str.fmt("Not enough samples: (%v / %v)", [Count!, samples]));

  return Sum! / math.itof(samples * 2);
};

